#!/bin/csh

#
# OBSOLETE?
#
# I(RJS) currently(2018-12-03) think this script is never used. I think it is an old version
# and not getting called. The actual RCS log archiving process is one of many jobs performed by
# /occ/bin/rcx as part of the RCS initialisation after a reboot.
#

source $DEPLOY_BIN/setup
source $DEPLOY_BIN/class_setup
source $DEPLOY_BIN/rcx_setup

set dd = `date -u +"%y_%m_%d"`

cd $DEPLOY_LOG

set list = `glob rcs_command*.txt rcs_ctrl*.txt rcs_cil*.txt rcs_task*.txt  rcs_oper*.txt  rcs_event* /occ/tmp/rcs_lt.log `
# The following log files are not included in the glob above. Do you want them kept?
# Note that SNF calls them "dros" and explicitly deletes them below.
#       rcs_ers_*.txt
#       rcs_iss_*.txt
#       rcs_ems_*.txt
#       scorelog_*.txt
# The following logs no longer seem to even exist, at least on the date I checked. They could be intermittent
#       rcs_command*.txt        has been replaced by rcs_cil*.txt?
#       rcs_ctrl*.txt
#       rcs_oper*.txt

if ($status == 0) then
    set ct = 0
    echo "Checking files for archiving..."  
    foreach file ( $list )
	mv -v $file tmp/
	@ ct++
    end
    echo "Moved $ct files for archiving"
    echo "Deleting dros..."
    /bin/rm -fv rcs_*.txt
#    /bin/rm -f oss_*.txt
else
    echo "No files to archive, Deleting dros..."
    /bin/rm -fv rcs_*.txt
 #   /bin/rm -f oss_*.txt
    exit 1
endif

cd tmp

set olist = `glob rcs_oper*.txt`

if ($status == 0) then
    echo "Generating Observation log..."
    cat $olist | grep -f/home/occ/greppy | /home/occ/awky >& /home/occ/obs_log_lt_$dd
else
    echo "No Operations log found"
endif

echo "Creating archive..."
tar cvf Ops_lt_rcs_${dd}.tar  *
gzip Ops_lt_rcs_${dd}.tar

set RELAY_HOST = 192.168.2.32
set RELAY_PORT = 6666

set ARCHIVE_HOST = 192.168.3.192
set ARCHIVE_PORT = 6666

set KEYSTORE   = /occ/misc/certs/pca.private
set TRUSTSTORE = /occ/misc/certs/itr.public
set PASS       = geronimo

java -Dcpdelim=@ ngat.net.SSLFileTransfer @client @id LT @key $KEYSTORE @trust $TRUSTSTORE @pass $PASS \
    @host $RELAY_HOST @port $RELAY_PORT @rhost $ARCHIVE_HOST @rport $ARCHIVE_PORT @band 5 \
    @fwd Ops_lt_rcs_${dd}.tar.gz  @dest tmc/Ops_lt_rcs_${dd}.tar.gz

java -Dcpdelim=@ ngat.net.SSLFileTransfer @client @id LT @key $KEYSTORE @trust $TRUSTSTORE @pass $PASS \
    @host $RELAY_HOST @port $RELAY_PORT @rhost $ARCHIVE_HOST @rport $ARCHIVE_PORT @band 5 \
    @fwd /home/occ/obs_log_lt_$dd  @dest tmc/obs_log_lt_$dd


echo "Deleting files..."
/bin/rm -fv rcs_*.txt
echo "Moving logs to log archive..."
mv -v Ops_lt_rcs_${dd}.tar.gz $DEPLOY_LOG/archive/

